; TOML
; Barry Arthur, 2014 04 19

.skip_white   = false
.namespace    = 'tomlp'
.parser_name  = 'toml'
.root_element = 'tomldoc'

tomldoc    ::= (as ( key_group | key_value ))+
key_group  ::= '[' key_name ']'                                 -> #key_group
key_name   ::= identifier ( '\.' identifier )*                    -> #key_name
identifier ::= '\$\?[a-zA-Z][a-zA-Z0-9_]*\%(?\|!\|#\)\?'
key_value  ::= identifier s '=' as value                 -> #key_value
value      ::= primitive | array
primitive  ::= float | datetime | boolean | integer | string
float      ::= '\%(+\|-\)\?[0-9]\+\.[0-9]\+'                      -> #float
datetime   ::= date 'T' time 'Z'                                  -> #datetime
date       ::= '[0-9]\{4}-[0-9]\{2}-[0-9]\{2}'
time       ::= '[0-9]\{2}:[0-9]\{2}:[0-9]\{2}'
boolean    ::= 'true\|false'
integer    ::= '\%(+\|-\)\?[0-9]\+'                               -> #integer
string     ::= '"\%(\\.\|[^\\"]\)\{-}"'                           -> #string
array      ::= as '[' as value ( as ',' as value )* as ','? as ']' as -> #array
as         ::= s '#.\{-}\n'? '\_s*'                             -> #comment
s          ::= '\s*'
